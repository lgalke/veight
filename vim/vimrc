set nocompatible

syntax on
filetype plugin indent on

" Section: Basic Options {{{
set autowrite
set foldmethod=marker
set showtabline=2
set guioptions-=e "recommended by flagship
set ignorecase smartcase
set showcmd
set showmatch
set wrap
if has("linebreak")
  " dont break in the middle of a word
  set linebreak
  " ident line breaks
  set breakindent
  let &showbreak = "+++ "
endif
set nolist
set number relativenumber
set visualbell
set listchars=eol:¶,tab:¦-,trail:±,extends:»,precedes:«,nbsp:¬
" better default comment string for a lot of configuratoin files
set commentstring=#\ %s
set noruler
set dictionary+=/usr/share/dict/words
" %=%-14.(%l,%c%V%)\ %P
if has('persistent_undo')
  set undofile	" keep an undo file (undo changes after closing)
endif
let g:tex_flavor = 'latex'

" wild menu
" typescript development
set wildignore+=*.js,*.map
set wildmode=longest:full,full

" vim8 specific
if v:version > 800
  set signcolumn=yes
endif
" }}}
" Section: Maps {{{
if has('conceal')
  set conceallevel=2 concealcursor=
endif

let mapleader = ","
let maplocalleader = "\\"

" movement
map <Tab> %
nnoremap H ^
nnoremap L $
" command line
noremap + :
noremap @+ @:
" modes
" Convenience
vnoremap <Space> I<Space><Esc>gv
nnoremap <Space> za
inoremap <C-C> <Esc>`^
nnoremap <C-S> :w<cr>


"Quick access
nnoremap <leader>vv :Vedit vimrc<cr>
nnoremap <leader>vx :edit ~/.vim/ftplugin<cr>
"}}}
" Section: Commands {{{
" command! -complete=packadd -nargs=1 Packadd packadd <args> | write | edit %
command! -bar -bang -complete=packadd -nargs=1 Packadd packadd<bang> <args> | doautoall BufRead
command! -bar -nargs=0 Helptags silent! helptags ALL

function! s:Tags() abort
  silent let dir = system("git rev-parse --show-toplevel")
  if v:shell_error
    let tagsfile = "tags"
  else
    let tagsfile = dir . "/.git/tags"
  endif
  let cmd = ['ctags', '-R', '-f', tagsfile]
  let job = job_start(cmd)
endfunction

command! -bar -nargs=0 Tags call <SID>Tags()
" }}}
" Section: Autocmds {{{
if has("autocmd")
  augroup veight
    au!
    " q enough
    autocmd FileType help nnoremap <buffer> q :q!<cr>
    " as recommended to not write ugly mails for others
    autocmd FileType mail setlocal formatoptions+=aw
    " make useful dispatch
    autocmd FileType pandoc let b:dispatch=":Pandoc pdf"
    autocmd FileType dot let b:dispatch="dot -Tpdf -o %:r.pdf %"
    " guess the dispatch
    autocmd BufReadPost * if getline(1) =~# '^#!' | let b:dispatch = getline(1)[2:-1] . ' %' | let b:start = b:dispatch | endif
    autocmd FileType html setlocal foldmethod=marker foldmarker=<div,/div> iskeyword+=-
    " autocmd FileType pandoc,tex let b:AutoPairs = {'(':')','[':']','{':'}', '$':'$', '`':'`'}
    if exists("+omnifunc")
      autocmd Filetype * if &omnifunc == "" | setlocal omnifunc=syntaxcomplete#Complete | endif
    endif
    autocmd FileType tex syn sync minlines=500 maxlines=2000
          \ | let b:surround_{char2nr('x')} = "\\texttt{\r}"
          \ | let b:surround_{char2nr('l')} = "\\\1identifier\1{\r}"
          \ | let b:surround_{char2nr('e')} = "\\begin{\1environment\1}\n\r\n\\end{\1\1}"
          \ | let b:surround_{char2nr('v')} = "\\verb|\r|"
          \ | let b:surround_{char2nr('V')} = "\\begin{verbatim}\n\r\n\\end{verbatim}"
    autocmd FileType tex,mail if exists(':Thesaurus') | setlocal keywordprg=:Thesaurus | endif
    au VimEnter * if exists(':RainbowParenthesesActivate') | RainbowParenthesesActivate | endif
    au Syntax * if exists(':RainbowParenthesesLoadRound') | RainbowParenthesesLoadRound | endif
    au Syntax * if exists(':RainbowParenthesesLoadSquare') | RainbowParenthesesLoadSquare | endif
    au Syntax * if exists(':RainbowParenthesesLoadBraces') | RainbowParenthesesLoadBraces | endif
    au Syntax * if exists(':RainbowParenthesesLoadChevrons') | RainbowParenthesesLoadChevrons | endif
  augroup END
endif
" }}}
" Section: Core {{{
" Markdown {{{
let g:markdown_fenced_languages = ['html', 'python', 'bash=sh']
" }}}
" Flagship {{{
" this is hacky to fix with to 2
set statusline=%#SignColumn#%-2.2(%M\ %)%*
set statusline+=%#CursorLineNr#%4.4(%c%)%*
" buffer number and modified
set statusline+=[b%n\ %f%(\ *%{fugitive#head()}%)]
" file
" usual stuff
set statusline+=\ %H%R
set statusline+=%=
augroup veight_flagship
  au!
  " this is necessary because (vim-signify|vim-gitgutter) somehow breaks colors
  let hl_as_usual = {"hl": ['Statusline', 'StatusLineNC']}
  " autocmd User Flags call Hoist("buffer", -10, hl_as_usual, function('fugitive#statusline'))
  autocmd User Flags call Hoist("window", +10, {"hl": ['WarningMsg','StatusLineNC']}, 'SyntasticStatuslineFlag')
  autocmd User Flags call Hoist("buffer", 0, hl_as_usual, '%{g:asyncrun_status}')
  autocmd User Flags call Hoist("window", -10, hl_as_usual, "%{tagbar#currenttag('[%s]', '')}")
augroup END
" }}}
" Fugitive {{{
nmap <leader>gs :Gstatus<cr>
nmap <leader>gw :Gwrite<cr>
nmap <leader>gc :Gcommit<cr>
nmap <leader>gp :Gpush<cr>
nmap <leader>gf :Gfetch<cr>
nmap <leader>gm :Gmerge<cr>
" }}}
" Tabular {{{
nnoremap <leader>t= :Tabularize /=/
nnoremap <leader>t& :Tabularize /&/
" }}}
" CtrlP {{{
let g:ctrlp_map = '<leader>,'
nnoremap <leader>. :CtrlPTag<cr>
nnoremap <leader>b :CtrlPBuffer<cr>
let g:ctrlp_extensions = ['tag']
" }}}
" }}}
" Section: Extra {{{
" XPTemplate {{{
let g:xptemplate_vars="$author=Lukas\ Galke&$email=vim@lpag.de"
" }}}
" PyMode {{{
let g:pymode_python = 'python3'
" }}}
" Dispatch {{{
nnoremap <leader>m :Make<cr>
nnoremap <leader>d :Dispatch<cr>
" }}}
" Ragtag {{{
let g:ragtag_global_maps = 1
" }}}
" Vimtex {{{
let g:vimtex_latexmk_continuous = 0
let g:vimtex_latexmk_background = 1
let g:vimtex_latexmk_callback = 0

" let g:vimtex_imaps_leader = '`'

let g:vimtex_view_general_viewer = 'okular'
let g:vimtex_view_general_options = '--unique @pdf\#src:@line@tex'
let g:vimtex_view_general_options_latexmk = '--unique'

let g:vimtex_fold_enabled = 1
let g:vimtex_fold_preamble = 1
let g:vimtex_fold_comments = 1
let g:vimtex_indent_enabled = 1
let g:vimtex_indent_bib_enabled = 1
let g:vimtex_format_enabled = 1 " this did not work well, recheck if fixed
augroup vimtex_mappings
  au!
  au User VimtexEventInitPost nmap <F3> <plug>(vimtex-toc-toggle)
augroup END
" }}}
" Syntastic {{{
let g:syntastic_check_on_open            = 1
let g:syntastic_check_on_wq              = 0
let g:syntastic_auto_jump                = 0
" dont clutter the loc list
let g:syntastic_always_populate_loc_list = 0
let g:loc_list_height                    = 5
let g:syntastic_aggregate_errors         = 1
let g:syntastic_id_checkers              = 1
let g:syntastic_auto_loc_list            = 0
" invoke error loc list manually
nmap <silent> <leader>e :Errors<CR>
" tex checker
let g:syntastic_tex_checkers = ["chktex", "lacheck"]
" 1: Cmd terminated with space
" 8: Wrong type of dashes
" 36: spaces around braces
let g:syntastic_tex_chktex_args = "-n1 -n8 -n36"
" python checker
let g:syntastic_python_checkers = ['python', 'flake8']
let g:syntastic_python_python_exec = '/usr/bin/python3'
let g:syntastic_python_flake8_exec = '/usr/bin/python3'
let g:syntastic_python_flake8_args = '-m flake8 --ignore=E501,E203'
" }}}
" UltiSnips {{{
let g:UltiSnipsSnippetsDir = "~/.vim/UltiSnips"
let g:UltiSnipsListSnippets = "<c-r><c-r><c-j>"
let g:UltiSnipsExpandTrigger = "<c-j>"
let g:UltiSnipsJumpForwardTrigger = "<c-j>"
let g:UltiSnipsJumpBackwardTrigger = "<c-k>"
inoremap <c-x><c-k> <c-x><c-k>
" }}}
" }}}
" Section: Community {{{
" dotoo
let g:dotoo#agenda#files = ['~/.plan/*.dotoo', '~/git/vec4ir/vec4ir.dotoo']
" pandoc
let g:pandoc#filetypes#pandoc_markdown = 0
let g:pandoc#filetypes#handled         = ["extra", "pandoc", "rst", "textile"]
let g:pandoc#modules#disabled          = ["menu"]
let g:pandoc#syntax#conceal#urls       = 1
let g:pandoc#completion#bib#mode       = 'citeproc'
" table mode (pandoc table compatible) {{{
let g:table_mode_corner                = '+'
let g:table_mode_seperator             = '|'
let g:table_mode_fillchar              = '-'
nmap <leader>,tr :TablemodeRealign<cr>
" }}}
" }}}
" Section: Testing {{{
" Async {{{ "
nnoremap <leader>at :AsyncRun ctags -R<cr>
nnoremap <leader>ao :AsyncRun okular %:r.pdf
" }}} Async "
" Signify {{{
let g:signify_vcs_list = [ 'git' ]
let g:signify_line_highlight = 0
" }}}
" TagBar {{{
nmap <F2> :TagbarToggle<CR>
" }}}
" VimCompletesMe + vimtex {{{
augroup VimCompletesMeTex
  autocmd!
  autocmd FileType tex let b:vcm_omni_pattern =
        \ '\v\\%('
        \ . '\a*cite\a*%(\s*\[[^]]*\]){0,2}\s*\{[^}]*'
        \ . '|\a*ref%(\s*\{[^}]*|range\s*\{[^,}]*%(}\{)?)'
        \ . '|hyperref\s*\[[^]]*'
        \ . '|includegraphics\*?%(\s*\[[^]]*\]){0,2}\s*\{[^}]*'
        \ . '|%(include%(only)?|input)\s*\{[^}]*'
        \ . '|\a*(gls|Gls|GLS)(pl)?\a*%(\s*\[[^]]*\]){0,2}\s*\{[^}]*'
        \ . '|includepdf%(\s*\[[^]]*\])?\s*\{[^}]*'
        \ . '|includestandalone%(\s*\[[^]]*\])?\s*\{[^}]*'
        \ . ')'
augroup END
" }}}
" }}}
" the packs {{{ "
" need to load vimtex as usual

" Provides :Man
runtime! ftplugin/man.vim
set keywordprg=:Man

if v:version > 703 && (has('python3') || has('python'))
  packadd python-mode
endif

if !has('packages')
  " BACKWARDS COMPATIBLE
  runtime pack/core/opt/vim-pathogen/autoload/pathogen.vim
  echom "no +packages compat mode by infecting with pathogen"
  execute pathogen#infect('pack/core/start/{}' , 'pack/extra/start/{}' , 'pack/community/start/{}' , 'pack/testing/start/{}')
  execute pathogen#infect('pack/core/opt/{}'   , 'pack/extra/opt/{}'   , 'pack/community/opt/{}'   , 'pack/testing/opt/{}')
else
  packadd vim-pathogen
endif
execute pathogen#infect('bundle/{}')

" letz try to run xptemplate all the time
packadd xptemplate

if has('syntax') && has('eval')
  packadd matchit
endif
" }}} the packs "


if !exists('$TMUX') && v:version > 800
  " this should only be used if outside tmux
  set termguicolors
endif

if filereadable('Session.vim') && argc() == 0
  " restore session if file is present in directory and vim was
  " started without arguments, do BufRead autocommands after
  source Session.vim
endif

set background=dark
silent! colo gruvbox
